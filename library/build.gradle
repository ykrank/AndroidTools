apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.dcendents.android-maven'

group='com.github.ykrank.AndroidTools'

android {
    compileSdkVersion 28
	buildToolsVersion rootProject.ext.buildToolsVersion
	
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true

        consumerProguardFiles 'proguard-rules.pro'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dataBinding {
        enabled = true
    }

    defaultPublishConfig "normalRelease"

    flavorDimensions "market"
    productFlavors {
        play{
            dimension "market"
        }
        normal{
            dimension "market"
        }
    }
}

kapt {
    mapDiagnosticLocations = true
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.javaDirectories
}

artifacts {
    archives sourcesJar
}

dependencies {
    normalApi fileTree(dir: 'libs/normal', include: ['*.jar'])
    playApi fileTree(dir: 'libs/play', include: ['*.jar'])
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    //For jackson
    api "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.10.2'

    api "com.google.android.material:material:1.2.0-alpha04"
    api "androidx.cardview:cardview:$rootProject.androdiXVersion"
    api "androidx.percentlayout:percentlayout:$rootProject.androdiXVersion"
    api "androidx.legacy:legacy-preference-v14:$rootProject.androdiXVersion"
    api "androidx.legacy:legacy-support-v4:$rootProject.androdiXVersion"
    api 'androidx.constraintlayout:constraintlayout:1.1.3'

    api 'androidx.multidex:multidex:2.0.1'

    debugApi 'com.squareup.leakcanary:leakcanary-android:2.1'
    releaseApi 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.3'
    api 'org.apache.commons:commons-lang3:3.9'
    api 'com.google.guava:guava:28.2-android'

    api 'io.reactivex.rxjava2:rxjava:2.2.17'
    api 'io.reactivex.rxjava2:rxandroid:2.1.1'
    api 'io.reactivex.rxjava2:rxkotlin:2.4.0'

    api 'com.github.bumptech.glide:glide:4.11.0'
    api 'com.github.bumptech.glide:okhttp3-integration:4.11.0'
    //okhttp 3.12.0 have bug https://github.com/square/okhttp/issues/4395
    api 'com.squareup.okhttp3:okhttp:4.2.2'

    api 'com.shizhefei:LargeImageView:1.1.0'
    api 'com.github.ykrank:androidautodispose:0.4.0'
    api 'com.hannesdorfmann:adapterdelegates3:3.1.0'

    playApi 'com.tencent.bugly:crashreport:3.0.1'
    normalApi 'com.tencent.bugly:crashreport_upgrade:1.4.2'

    api 'com.orhanobut:logger:2.2.0'

    //FilePicker
    api 'com.nononsenseapps:filepicker:4.1.0'

    //PictureSelector
    compileOnly 'com.github.LuckSiege.PictureSelector:picture_library:v2.2.5'

    //PictureUpload
    compileOnly 'org.jsoup:jsoup:1.12.1'
    compileOnly 'com.squareup.retrofit2:retrofit:2.7.1'
    compileOnly 'com.squareup.retrofit2:adapter-rxjava2:2.7.1'
    compileOnly 'com.squareup.retrofit2:converter-jackson:2.7.1'
    compileOnly 'com.squareup.retrofit2:converter-scalars:2.7.1'
}

if (android.productFlavors.size() > 0) {
    android.libraryVariants.all { variant ->
        if (variant.name.toLowerCase().contains("debug")) {
            return
        }

        def bundleTask = tasks["bundle${variant.name.capitalize()}Aar"]

        artifacts {
            archives(bundleTask.archivePath) {
                classifier variant.flavorName
                builtBy bundleTask
                name = project.name
            }
        }

    }
}